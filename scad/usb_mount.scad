//translate([0,-12,0])import("../stl/pica-mount.stl");
plate_thick=3.5;
plate_l=70;
plate_w=79;
post_h=16;
cut_out_r=2.66;
holder_y=38;
holder_x=19.25;
holder_w=10;
pcb_t=1.8;
$fa=.5;
$fs=.5;
module num8counter()
//countersink shape
{
    hh=3.6;
    rr=8.6/2;
    num8slot=4.8;
    ep=0.01;
    foot_t=15;
    translate([0,0,-hh])
    {
        cylinder(r1=num8slot/2,r2=rr,h=hh,$fn=24);
        translate([0,0,-foot_t])cylinder(r1=num8slot/2,r2=num8slot/2,h=hh+foot_t,$fn=24);
        translate([0,0,hh-ep])cylinder(r=rr,h=foot_t,$fn=24);
    }
}
module base_plate()
{
    translate([plate_l/2,-plate_w/2,plate_thick/2])cube([plate_l,plate_w,plate_thick],center=true);
}
module screw_cut()
{
    //posts
    rr=3.5/2;
    translate([20-20,0,0])
    {
    translate([25,-12,-1])cylinder(r=rr,h=post_h+2);
    translate([25,-12-52,-1])cylinder(r=rr,h=post_h+2);
    }
    translate([25,-64-10,plate_thick/4-.01])translate()cube([rr*2,20,plate_thick/2],center=true);
}
module mount_post()
{
    rr1=6;
    rr2=3.5;
    translate([20,0,0])
    {
    translate([25,-12,0])cylinder(r1=rr1,r2=rr2,h=post_h);
    translate([25,-12-52,0])cylinder(r1=rr1,r2=rr2,h=post_h);
    }
}
module tie_cut()
{
    translate([10-20,7,0])
    {
    translate([20,-15,0])cube([8,2,20],center=true);
    translate([20,-33,0])cube([8,2,20],center=true);
    }
    translate([25,-5,0])cube([8,2,20],center=true);
    //translate([25,-71,0])cube([8,2,20],center=true);
}
module wigglestop()
{
    hh=post_h+pcb_t+4;
    xx=25-holder_x;
    translate([xx,-holder_y,hh/2])difference()
    {
        cube([6,holder_w,hh],center=true);
        translate([3,0,pcb_t/2-hh/2+post_h])cube([6,holder_w*2,pcb_t],center=true);
        translate([3,0,6/sqrt(2)-2.5])translate([0,0,pcb_t/2-hh/2+post_h])rotate([0,-30,0])cube([6,holder_w*2,pcb_t],center=true);
        translate([2+3-.25,0,6/sqrt(2)-1.9])translate([0,0,pcb_t/2-hh/2+post_h])rotate([0,-30,0])cube([6,holder_w*2,pcb_t],center=true);
    }
}
module final()
{
    difference()
    {
        union()
        {
            base_plate();
            translate([-20,0,0])
            {
            mount_post();
            translate([55,-73.5,plate_thick-.05])scale([1,.75,1])rotate([0,0,90])spine();
            }
            wigglestop();
            translate([0,-10,0])screw_post();
            translate([plate_l,-10,0])screw_post();
            translate([plate_l,-(plate_w-10),0])screw_post();
            translate([0,-(plate_w-10),0])screw_post();
        }
        screw_cut();
        tie_cut();
        translate([0,-10,0])mount_screw_hole();
        translate([plate_l,-10,0])mount_screw_hole();
        translate([plate_l,-(plate_w-10),0])mount_screw_hole();
        translate([0,-(plate_w-10),0])mount_screw_hole();
    }
}
final();
module spine()
{
    translate([10,0,0])
    {
    difference()
    {
        cube([71,20,5]);
        translate([0,-13.5,2-.1])spine_cut();
        translate([0,13.5,2-.1])spine_cut(); 
    }
}
}
module spine_cut()
{
    minkowski()
    {
        cube([100,20,5]);
        sphere(r=2);
    }
}
module screw_post()
{
    cylinder(r=14/2,h=12);
}
module mount_screw_hole()
{
    translate([0,0,11])num8counter();
}
//spine();